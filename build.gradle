plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'net.nemerosa.versioning' version '2.8.2'
    id 'java'
}

group 'com.tristandyer'
version '1.0.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'com.sparkjava:spark-core:2.9.1'
    compile 'org.slf4j:slf4j-nop:1.7.21'
    compile name: 'Alloy-5.0.0.1'
    compile name: 'apple-osx-ui'
}

sourceSets {
    main {
        resources {
            srcDirs "src/main/resources"
        }
    }
}

jar {
    manifest {
        from(
            zipTree('libs/Alloy-5.0.0.1.jar').find{ it.name == 'MANIFEST.MF' }
        )
    }
}

shadowJar {
    baseName = 'Sterling'
    classifier = null
    manifest {
        attributes(
                'Main-Class'         : 'sterling.Sterling',
                'Built-By'           : System.properties['user.name'],
                'Build-Timestamp'    : new java.text.SimpleDateFormat("yyy-MM-dd").format(new Date()),
                'Build-Revision'     : versioning.info.commit,
                'Created-By'         : "Gradle ${gradle.gradleVersion}",
                'Build-Jdk'          : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS'           : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
                'Build-Version'      : version
        )
    }
}
